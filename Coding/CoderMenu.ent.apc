{Application 'CODERMENU' logic file generated by CSPro}
PROC GLOBAL
{
TODO: Set data file as a central data file Look into programmatically changing the data file based on round and year
}
//--------------------------------Variable Declaration---------------------------------------
string	ftpip,			//Stores ftp address
		ftpun,			//stores ftp username
		ftppw,			//stores ftp password
		cswebip,		//Stores csweb address
		username,		//stores user name
		status,			//stores current login status
		round_ed		//stores round and ed
		;
		
numeric	errflag,		//sets error flag
		roundnumber,	//saves round number
		indivcounttot,	//counts individuals
		denominator,	//number of households (for calculating completed eds)
		numerator		//number of completed households (for calculating completed eds)
		;

file	loginfile,		//saves login status in a file	
		pffFile		//Creates a .pff file
		;
		
//-------------------------------Array Declaration------------------------------------------
array string	vsCode(99);		//valueset codes
array string 	vsLabel(99);	//valueset labels
		
//-----------------------------Function declaration-----------------------------------------
//Launches a questionnaire for coding
function launchQuestionnaire()
	string pffFilename = Pathname(application) + "DataEntry.pff";
	setfile(pffFile,pffFilename,create);

	filewrite(pffFile,"[Run Information]");
	filewrite(pffFile,"Version=CSPro 7.1");
	filewrite(pffFile,"AppType=Entry");

	filewrite(pffFile,"[DataEntryInit]");
	filewrite(pffFile,"OperatorID=%s",UNAME);
	filewrite(pffFile,"StartMode=Modify;%s",CHOOSE_QUESTIONNAIRE);
	filewrite(pffFile,"Lock=Add,Verify,Stats");
	filewrite(pffFile,"FullScreen=Yes");
	filewrite(pffFile,"AutoAdd=No");

	filewrite(pffFile,"[Files]");
	filewrite(pffFile,"Application=%s","./DataEntry.ent");
	filewrite(pffFile,"InputData=%s","../Data/data1.csdb|CSPRODB");

	filewrite(pffFile,"[ExternalFiles]");
	filewrite(pffFile,"AUXIL_DICT=%s","../Data/auxil.csdb|CSPRODB");
	filewrite(pffFile,"INDUSTRY_CODES_DICT=%s","../Data/IndustryCodes.csdb|CSPRODB");
	filewrite(pffFile,"OCCUPATION_CODES_DICT=%s","../Data/OccupationCodesBARSOC.csdb|CSPRODB");
	
	filewrite(pffFile,"[Parameters]");
	filewrite(pffFile,"CODER=%s",maketext("%v",visualvalue(USERID)));
	filewrite(pffFile,"UNAME=%s",UNAME);

	close(pffFile);

	execpff(filename(pffFile));
end;

//downloads Data based on ED
function downloadData()
	errflag = 0;
//start ftp download
	{errmsg("Attempting to download all data from Round :%d, ED :%d",CO_ROUND,CO_ED);
	//errmsg("Data dictionary value Round :%V, ED :%V", RNDNO,EDNO);
	if syncconnect(FTP,ftpip,ftpun,ftppw) then		//connect to FTP when outside of the government WAN
		if not syncdata(GET,LABOURFORCE_DICT,maketext("%2d%v",CO_ROUND,CO_ED)) then		//get labourforce data for round and ED
			inc(errflag);
		endif;
		syncdisconnect();		//disconnect
	else
		inc(errflag,2);
	endif;}
//end ftp download
//start csweb download
	errmsg("Attempting to download all data from Round :%d, ED :%d",CO_ROUND,CO_ED);
	//errmsg("Data dictionary value Round :%V, ED :%V", RNDNO,EDNO);
	if syncconnect(CSWeb,cswebip,ftpun,ftppw) then		//connect to FTP when outside of the government WAN
		if not syncdata(GET,LABOURFORCE_DICT,maketext("%2d%v",CO_ROUND,CO_ED)) then		//get labourforce data for round and ED
			inc(errflag);
		endif;
		syncdisconnect();		//disconnect
	else
		inc(errflag,2);
	endif;
//end csweb download
	if errflag > 1 then
		errmsg("Download of round %d, ED %d failed, Please try again. %d errors were found",CO_ROUND,CO_ED,errflag);
	elseif errflag = 0 then
		errmsg("Download of round %d, ED %d was successful",CO_ROUND,CO_ED);
	elseif errflag = 1 then
		errmsg("Download of round %d, ED %d failed, Please try again",CO_ROUND,CO_ED);
	endif;
end;

//uploads data to server
function saveWork()
	{if not roundnumber in 1:4 then
		roundnumber = 0;
		do until roundnumber in 1:4
			roundnumber = accept("Choose current round","One","Two","Three","Four");
		enddo;
	endif;}
	errflag = 0;
	//errmsg("Attempting to download all data from Round :%d, ED :%d",CHOOSE_ROUND,CO_ED);
	//errmsg("Data dictionary value Round :%V, ED :%V", RNDNO,EDNO);
//start ftp upload
	{if syncconnect(FTP,ftpip,ftpun,ftppw) then		//connect to FTP when outside of the government WAN
		if not syncdata(PUT,LABOURFORCE_DICT,maketext("%5s",round_ed)) then		//upload labourforce data for round and ED
			inc(errflag);
		endif;
		syncdisconnect();		//disconnect
	else
		inc(errflag,2);
	endif;}
//end ftp upload
//start csweb upload
	if syncconnect(CSWeb,cswebip,ftpun,ftppw) then		//connect to FTP when outside of the government WAN
		if not syncdata(PUT,LABOURFORCE_DICT,maketext("%5s",round_ed)) then		//upload labourforce data for round and ED
			inc(errflag);
		endif;
		syncdisconnect();		//disconnect
	else
		inc(errflag,2);
	endif;
//endcsweb upload
	if errflag >= 1 then
		errmsg("Upload of round %s, ED %s failed, Please try again. %d errors were found",round_ed[1:2],round_ed[3:5],errflag);
	//elseif errflag = 0 then
	//	errmsg("Upload of round %d, ED %d was successful",roundnumber,CO_ED);	
	endif;
end;

//Writes the login status to a file
function writeloginstatus(x)
	string loginFilename = pathname(application)+"loginflag.dat";
	setfile(loginfile,loginFilename,create);

	filewrite(loginfile,maketext("%d%02d%03d%02d%s",x,visualvalue(CO_ROUND),visualvalue(CO_ED),USERID,strip(UNAME)));	//write value of "x" to file. 1=logged in, 0=logged out
	setoperatorid(strip(UNAME));
	savesetting("UNAME",strip(UNAME));
	
	close(loginfile);
end;

//reads login status and returns 1 (logged in) or 0 (not logged in)
function readloginstatus()
	string loginfilename = pathname(application)+"loginflag.dat";	//set file path
	setfile(loginfile, loginfilename);		//set file handle
	fileread(loginfile, status);			//read file
	close(loginfile);						//close file
	
	readloginstatus = tonumber(status[1:1]);		//set function to file contents
	CO_ROUND = tonumber(status[2:2]);
	CO_ED = tonumber(status[4:3]);
	if tonumber(status[1:1]) = 1 then		//if logged in set login details
		USERID = tonumber(status[7:2]);
		UNAME = status[9];
		setoperatorid(status[9]);		//sets operator ID to username
		savesetting("UNAME",strip(UNAME));		//saves username as a setting so that other apps can access
	elseif tonumber(status[1:1]) = 0 then		//if logged out clear login details from form
		USERID = notappl;
		UNAME = "";
	endif;
end;

function OnStop();
	//SaveWork();
end;

PROC CODERMENU_FF

PROC CODERMENU_QUEST
preproc

	//Initialize ftp variables
	ftpip = "10.129.20.110";
	ftpun = "Census";
	ftppw = "Census2020ADM!N";
	cswebip = "http://10.129.20.110:8082/csweb/api";
	
//-----------------------------------------------------------------------
PROC UNAME
preproc
	//if already logged in then just login, if not, continue logging in
	if readloginstatus() = 1 then
		advance to MAIN_MENU;
	endif;

//--------------------------------------------------------------------
PROC USERID
//code to make sure that the valueset label matches Username
postproc
	if getlabel(USERID_VS1,$) = strip(UNAME) then
		writeLoginStatus(1);//success
	else
		errmsg("Username and ID don't match! Please try again");
		move to UNAME;
	endif;


//---------------------------------------------------------------
PROC CO_ROUND
onfocus
	//displays saved round number
	$ = tonumber(loadsetting("ROUND"));
	
postproc
	roundnumber = $;
	//saves the Round number as a setting
	savesetting("ROUND",maketext("%d",$));
PROC CO_ED
onfocus
	//displays saved ED  number

	$ = tonumber(loadsetting("ED")); 
	
postproc
	round_ed = maketext("%v%v",CO_ROUND,CO_ED);
	//saves the ED number as a setting
	savesetting("ED",maketext("%d",$));
	setoperatorid(UNAME);
	
	
//--------------------------------------------------------------------
PROC MAIN_MENU
onfocus
	$ = notappl;
	username = getlabel(UNAME_VS1,UNAME);
	HOUSEHOLD_COUNT = 0;
	indivcounttot = 0;
	HOUSEHOLD_COUNT = countcases(LABOURFORCE_DICT where EDNO = CO_ED and RNDNO = CO_ROUND);
	forcase LABOURFORCE_DICT where EDNO = CO_ED and RNDNO = CO_ROUND and VACANT = 2 do
		indivcounttot = indivcounttot + NUMBER_OF_PERSONS;		//count all persons in completed cases
	endfor;
	INDIVIDUAL_COUNT = indivcounttot;
	
	{//determine if ED is completed 
	denominator = countcases(ENUMERATIONREPORT_DICT where ERED_NO = CO_ED {and ERROUND = ME_ROUND} and datediff(WEEK_ENDING,sysdate("YYYYMMDD"),"m") <= 3);
	numerator = countcases(ENUMERATIONREPORT_DICT where ERED_NO = CO_ED {and ERROUND = ME_ROUND} and ERCOMPLETED = 1 and datediff(WEEK_ENDING,sysdate("YYYYMMDD"),"m") <= 3);
	//errmsg("numerator=%d, denominator=%d, %f",numerator,denominator,numerator/denominator);
	if numerator/denominator >= 0.5 then		//if more than 50% of cases are marked as complete (Should always be either 0 or 1)
		ED_STATUS = "COMPLETE";
	elseif numerator/denominator < 0.5 then
		ED_STATUS = "ONGOING";
	endif;}
	
postproc
	if $ = 1 then
		downloadData();
	elseif $ = 2 then
		skip to CHOOSE_QUESTIONNAIRE;
	elseif $ = 3 then
		SaveWork();
	elseif $ = 9 then
		UNAME = "";
		USERID = notappl;
		writeloginstatus(0);
		stop(1);
	endif;
reenter;
PROC CHOOSE_QUESTIONNAIRE
onfocus
	numeric nextEntry = 1;
	numeric headindex;
	numeric nohead = 0;		//flagged when no head is found in the household when there should be. 0 = false, 1 = true
	forcase LABOURFORCE_DICT where EDNO = CO_ED and VACANT = 2 and RNDNO = CO_ROUND do
		loadcase(LABOURFORCE_DICT,RNDNO,EDNO,PARNO,STRATUM,HHNO);
		if CODER = notappl then		//and was not coded
			headindex = seek(LRTH = 0);
			if headindex = 0 then	//if no head was found
				nohead = 1;		//no head found = true
				vscode(nextEntry) = maketext("%V%V%V%V%V",RNDNO,EDNO,PARNO,STRATUM,HHNO);
				vslabel(nextEntry) = maketext("No person found-%02d-%03d-%03d-%d-%03d",RNDNO,EDNO,PARNO,STRATUM,HHNO );
				inc(nextEntry);
			else
				vscode(nextEntry) = maketext("%V%V%V%V%V",RNDNO,EDNO,PARNO,STRATUM,HHNO);
				vslabel(nextEntry) = maketext("%S-%02d-%03d-%03d-%d-%03d",strip(LLAST_NAME(headindex)),RNDNO,EDNO,PARNO,STRATUM,HHNO );
				inc(nextEntry);
			endif;
		else	
			headindex = seek(LRTH = 0);					//was coded
			if headindex = 0 then	//if no head was found
				nohead = 1;		//no head found = true
				vscode(nextEntry) = maketext("%V%V%V%V%V",RNDNO,EDNO,PARNO,STRATUM,HHNO);
				vslabel(nextEntry) = maketext("No person found-%02d-%03d-%03d-%d-%03d",RNDNO,EDNO,PARNO,STRATUM,HHNO );
				inc(nextEntry);
			else
				vscode(nextEntry) = maketext("%V%V%V%V%V",RNDNO,EDNO,PARNO,STRATUM,HHNO);
				vslabel(nextEntry) = maketext("%S-%02d-%03d-%03d-%d-%03d: Coded (%v)",strip(LLAST_NAME(headindex)),RNDNO,EDNO,PARNO,STRATUM,HHNO, getlabel(CODER_VS1, CODER) );
				inc(nextEntry);
			endif
		endif;
	endfor;
	
	vscode(nextEntry) = "";
	setvalueset($,vsCode,vsLabel);
	
	if nohead = 1 then		//if a household was found with no head
		errmsg("Households were found with missing person records. Please confirm with %V that the ED was completed", strip(getlabel(ENMRTR_VS1,ENMRTR)));
	endif;
	
postproc
	if datediff(tonumber(diagnostics("releasedate")),20190910) > 0 then		//developers fixed the bug so this only runs my workaround if the app has not been updated to the latest bugfree version
		CHOOSE_QUESTIONNAIRE = maketext("%12.11s",CHOOSE_QUESTIONNAIRE);		//workaround to make this shit work I shouldn't have to hack this to make it work!
	endif;
	launchQuestionnaire();
	move to MAIN_MENU;
	

//---------------------------------------------------------
