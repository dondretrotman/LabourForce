{Application 'LABOURFORCEBATCH' logic file generated by CSPro}
{
TODO:	Check that son is male and daughter is female
		dynamically change variable names to SPSS names by doing the following:
			create a dictionary with the correct names
			write batch code to savedata to the new dictionary
}
PROC GLOBAL
//VARIABLES
numeric	denomID = 0,
		denomHHLIST = 0,
		denomDELETE = 0,
		denomINSERT = 0,
		;

PROC LABOURFORCE_FF

PROC IDS0_EDT
postproc
	//filter empty records
	inc(denomID);
	if RNDNO = notappl and 
	EDNO = notappl and
	PARNO = notappl and
	STRATUM = notappl and
	HHNO = notappl then
		errmsg("ID fields are empty. Deleting case") denom = denomID summary;
		skip case;
	endif;
	
//-----------------------------------------------------------------
PROC HOUSEHOLD_LISTING_EDT
postproc
	//filter empty households with no questionnaires
	inc(denomHHLIST);
	if HH_TYP = notappl and 
	DATE = notappl and 
	LENDDATE = notappl then
		errmsg("Household fields are empty. Deleting case") denom = denomHHLIST summary;
		skip case;
	endif;
	
//--------------------------------------------------------------------------------
	
PROC VACANT
postproc
	inc(denomINSERT);
	if $ = 1 and count(LABOURFORCE_REC_EDT) = 0 then		//if Vacant household and no person records found
		insert(LABOURFORCE_REC_EDT(1));		//insert a person record
		
		//fills vacant person record with data
		LRTH(1) = 9;
		LCOMPLETED(1) = 89;
		INTRVW(1) = 4;
		OCCPNCY(1) = 9;
		LNDTNURE(1) = 9;
		RELHD(1) = 9;
		MARSTAT(1) = 9;
		EDUCLEV(1) = 9;
		QUALLEV(1) = 99;
		PLACETR(1) = 99;
		TRAINEDFOROCC(1) = 9;
		PLACETRAINED(1) = 99;
		WORKED(1) = 9;
		REGJOB(1) = 9;
		REASABS(1) = 9;
		PAIDABS(1) = 9;
		TIMEABS(1) = 9;
		MAJACTV(1) = 9;
		SEEKING(1) = 9;
		METHOD(1) = 9;
		REASNSK(1) = 9;
		LSTLOOK(1) = 9;
		WILLING(1) = 9;
		ABLE(1) = 9;
		WRKWNTD(1) = 9;
		HRSWNTD(1) = 99;
		EVERWKD(1) = 9;
		LSTWRKD(1) = 9;
		FRSTLOOK(1) = 9;
		UNEMPLSA(1) = 9;
		USINCOME(1) = 9;
		U2SINCOM(1) = 9;
		UEARNGS(1) = 99;
		U2EARNGS(1) = 99;
		RSNPART(1) = 9;
		TWOJOBS(1) = 9;
		RSN2JOBS(1) = 9;
		EXPRNCE(1) = 9;
		EXPRNCE2(1) = 9;
		EARNGS(1) = 99;
		EARNGS2(1) = 99;
		AVGEARNS(1) = 99;
		AVGEARNS2(1) = 99;
		HRSWRKD(1) = 99;
		NHRSWRKD(1) = 99;
		RSNLESS(1) = 99;
		ADDWRK(1) = 9;
		KINDWRK(1) = 9;
		HPWORK(1) = 9;
		INACTWHN(1) = 9;
		WORKBEFO(1) = 9;
		SCINCOME(1) = 9;
		IN2INCOM(1) = 9;
		INEARNGS(1) = 99;
		IN2EARNG(1) = 99;
		errmsg("Person record inserted to vacant household") denom = denomINSERT summary;
	endif;
	
//------------------------------------------------------
PROC DELETE_FLAG
inc(denomDELETE);
if $ = 1 then		//if delete flag is set
	errmsg("Delete flag is set. Deleting case") denom = denomDELETE summary;
	skip case;		//skip (delete) the current case
endif;

//------------------------------------------------------------
PROC LASTLOOK
postproc
	//cheapest way to "rename" this variable. copy the value from this variable to one that has the required name.
	SKADDWRK = $;

//---------------------------------------------------------------
