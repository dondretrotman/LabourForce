{Application 'LABOURFORCE' logic file generated by CSPro}
{
Continuous Labour Force Sample Survey (CLFSS) CAPI application
Author: Dondre Trotman
Date: 2018/07/17
Version: 0.1

Notes:

Todo:
	Codes for the following must be added
		Occupation, industry, no of males/females, Years (date of birth and interview), sample no, visit no, interviewer, supervisor, enumerator, coder, number of earners, household size, country codes age range institution codes
	Skip patterns must be added

}
PROC GLOBAL
//----------------------------VARIABLE DECLARATION---------------------------------------------
numeric i,				//general variable
		tempage,		//for calcuating age
		;

PROC LABOURFORCE_FF

PROC LIDNO
preproc
	$ = curocc();		//automatically enter the ID
	noinput;
	
//--------------------------------------------------------
PROC LAGE

	//check that age corresponds to date of birth
	tempage = datediff(LDOB, sysdate("YYYYMMDD"), "Y");
		
	if $ <> tempage then
		errmsg("age (%d) does not agree with DOB (%s), please verify",$,edit("9999/99/99",LDOB))
		select("Correct the age", $, "Correct the Date of birth", LDOB);
	endif;

//---------------------------------------------------------------------------
PROC ADDPER
	if $ = 2 then
		endgroup;
	endif;

//----------------------------------------------------------------------
PROC HD_HH
	
	//if not head of household, skip to individual details 
	if $ = 2 then
		skip to INDIVNO;
	endif;

//---------------------------------------------------------------------------
PROC INDIVNO

	//TODO: prefill information based on household listing ID number
	i = seek(LIDNO = $);
	
	if i = 1 then
		SEX = LSEX(INDIVNO);
		RELHD = LRTH(INDIVNO);
		BIRTHD = LDOB(INDIVNO);
		AGE = LAGE(INDIVNO);
	else
		errmsg("No person with ID #%d was found, please try again",$);
		reenter;
	endif;
	
//---------------------------------------------------------------------------
PROC RELHD

	//compare to HD_HH to verify head of household
	if not ($ = 0 and HD_HH = 1) then	
		if not ($ in 1:8 and HD_HH = 2) then
			errmsg("Relation to head (%d) does not match with Head of shousehold status (%d)",$,HD_HH)
			select("Change relation to head",$,"Change head of household status",HD_HH);
		endif;
	endif;  

//---------------------------------------------------------------------------
PROC AGE

	//check that age corresponds to date of birth
	tempage = datediff(BIRTHD, sysdate("YYYYMMDD"), "Y");
		
	if $ <> tempage then
		errmsg("age (%d) does not agree with DOB (%s), please verify",$,edit("9999/99/99",BIRTHD))
		select("Correct the age", $, "Correct the Date of birth", BIRTHD);
	endif;

//---------------------------------------------------------------------------
PROC CTRAINING
	
	//if not currently being trained, skip to if ever trained
	if $ = 2 then
		skip to TRAINEDFOROCC;
	endif;
	
//---------------------------------------------------------------------------
PROC TRAINEDFOROCC

	//if never trained, skip to Worked last week
	if $ = 2 then
		skip to WORKED;
	endif;
	
//---------------------------------------------------------------------------
PROC WORKED

	//if worked, skip to full time or part time
	if $ = 1 then
		skip to FULLPART;
	endif;
	
//---------------------------------------------------------------------------
PROC REGJOB

	//if not absent, skip to Major activity
	if $ = 2 then
		skip to MAJACTV;
	endif;
	
//---------------------------------------------------------------------------
PROC TIMEABS

	//if has a job, skip to Full time or part time
	if WORKED = 1 then
		skip to FULLPART;
	endif;

//---------------------------------------------------------------------------
PROC SEEKING

	//if not seeking work, skip to reason for not seeking work
	if $ = 2 then
		skip to REASNSK;
	endif;
	
//-----------------------------------------------------------------
PROC METHOD

	//skip to last looked for work
	skip to LSTLOOK;
PROC LSTLOOK

	//if never looked of looked for 4 months or more, skip to Hoping for job
	if $ in 1,4 then
		skip to HPWORK;
	endif;
	
//-----------------------------------------------------------------
PROC WILLING

	//if not willing, skip to ever worked
	if $ = 2 then
		skip to EVERWKD;
	endif;
	
//---------------------------------------------------------------
PROC ABLE

	//if not able, skip to ever worked
	if $ = 2 then
		skip to EVERWKD;
	endif;
	
//---------------------------------------------------------------
PROC EVERWKD

	//if never worked, skip to when first looked for work
	if $ = 2 then
		skip to FRSTLOOK;
	endif;
	
//--------------------------------------------------------------------
PROC LSTWRKD

	//skip to last occupation
	skip to LSTOCCUP;

//-----------------------------------------------------------
PROC FULLPART

	//if full time, skip to more than one job
	if $ = 1 then
		skip to TWOJOBS;
	endif;
	
//-----------------------------------------------------------------
PROC TWOJOBS

	//if not more than one job, skip to employer status
	if $ = 2 then
		skip to EMPLSTAT;
	endif;
	
//---------------------------------------------------------------
PROC EMPL2STAT

preproc
	//skip if only one job
	if TWOJOBS = 2 then
		skip to OCCUP;
	endif;
	
//--------------------------------------------------------
PROC SOCCUP

preproc
	//skip if only one job
	if TWOJOBS = 2 then
		skip to INDUS;
	endif;
	
//--------------------------------------------------------
PROC SINDUS

preproc
	//skip if only one job
	if TWOJOBS = 2 then
		skip to EXPRNCE;
	endif;
	
//--------------------------------------------------------
PROC EXPRNCE2

preproc
	//skip if only one job
	if TWOJOBS = 2 then
		skip to EARNGS;
	endif;
	
//--------------------------------------------------------
PROC NHRSWRKD

	//end if worked 40 or more hours
	if HRSWRKD in 10,11 then
		endlevel;
	endif;

//-----------------------------------------------------------------------
		
PROC ADDWRK

	//if not willling to do additional work, end interview
	if $ = 2 then
		endlevel;
	endif;

//-----------------------------------------------------------------------
PROC HPWORK

	//if not hoping for job, skip to ever worked
	if $ = 2 then
		skip to WORKBEFO;
	endif;
	
//-----------------------------------------------------------
PROC WORKBEFO

	//if never worked before, skip to main sources of livelihood
	if $ = 2 then
		skip to SCINCOME;
	endif;
	
//---------------------------------------------------------------------
